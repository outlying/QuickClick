// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {

    ext.general = [
            "android": [
                    "sdk": [
                            "compile": 29 as Integer,
                            "min"    : 21 as Integer,
                            "target" : 29 as Integer
                    ]
            ],
            "version": [
                    "code": 1,
                    "name": "0.0.1"
            ]
    ]

    ext.versions = [
            "androidx": [
                    "appcompat": "1.1.0",
                    "core"     : "1.2.0"
            ],
            "kotlin"  : "1.3.70"
    ]

    repositories {
        google()
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:3.5.3'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}"
    }
}

allprojects {

    version = general.version.name

    repositories {
        google()
        jcenter()
    }
}

/**
 * Empty task assemble to trigger root jobs during normal builds
 */

task assemble() {}

/**
 * Update Travis file
 *
 * This will ensure that Travis file has up to date configuration that will match
 * project settings
 */
import org.apache.tools.ant.filters.ReplaceTokens

task updateTravisYml(type: Copy) {
    from 'dev/travis/.travis.yml'
    into '.'
    filter(ReplaceTokens, tokens: [
            ANDROID_SDK_COMPILE: general.android.sdk.compile.toString()
    ])
}

tasks.assemble.dependsOn("updateTravisYml")

/**
 * Removes build dir from root
 */
task clean(type: Delete) {
    delete rootProject.buildDir
}
